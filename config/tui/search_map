function! NvimIdeHighlightGlobalSearchPattern(toSearch)
    let @/ = a:toSearch
    let &hlsearch=1
    redraw
endfunction

function! NvimIdeRemoveGlobalSearchPatternHighlight()
    let @/ = ""
    noh
endfunction

function! NvimIdeGetCtrlOSpecialSymbolNoCond()
    return nvim_replace_termcodes("<C-o>", v:true, v:false, v:true)
endfunction

function! NvimIdeGetCtrlOSpecialSymbol(mode)
    return (a:mode == "i" ? NvimIdeGetCtrlOSpecialSymbolNoCond() : "")
endfunction

function! NvimIdeLocalSearch(mode, toSearch)
    call NvimIdeHighlightGlobalSearchPattern(a:toSearch)
    " TODO: to add 'expand("<cword>")' into search history only after non-null search

    call feedkeys(NvimIdeGetCtrlOSpecialSymbol(a:mode) . "/")
endfunction

" Search in bufer
"     Call search
set ignorecase
set smartcase
nnoremap <C-F> :noh <bar> call NvimIdeLocalSearch("n", expand("<cword>"))<CR>
inoremap <C-F> <C-O>:noh <bar> call NvimIdeLocalSearch("i", expand("<cword>"))<CR>
snoremap <C-F> <C-O>y <bar> : call NvimIdeLocalSearch("s", @")<CR>

"     Continue search
" TODO: nnoremap works incorrect: when cursor in (1,1) pos, then it dos not search (possibly stop after <Left> failure)
nnoremap <f3> <Left>nzzgn<C-g>
inoremap <f3> <Left><C-O>n<C-O>zz<C-O>gn<C-g>
smap <f3> <Esc><f3>
cnoremap <f3> <CR>

" Search files
function! NvimIdeFindFiles()
    let l:dir = getcwd()
    if exists("g:nvim_ide_project_root")
        let l:dir = g:nvim_ide_project_root
    endif
    call fzf#run(fzf#wrap({'source': 'cd ' . l:dir . '; ' . g:nvim_ide_fzf_source,
                                   \ 'options': '--prompt="File> " --multi --history='.NvimIdeGetProjectExtraFilesDir().'/fzf_search_files_history '.
                                   \            '--bind=' . g:nvim_ide_fzf_bind,
                                   \ 'dir': l:dir}))
endfunction

inoremap <C-k> <C-o>:call NvimIdeFindFiles()<CR>
nnoremap <C-k> :call NvimIdeFindFiles()<CR>

"     Call search
nnoremap <M-f> : let &hlsearch = NvimIdeSearchInFilesContent("")<CR>
inoremap <M-f> <C-o>: let &hlsearch = NvimIdeSearchInFilesContent("")<CR>
snoremap <M-f> <C-o>y <bar> : let &hlsearch = NvimIdeSearchInFilesContent(getreg())<CR>
