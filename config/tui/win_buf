" To change current directory automatically according to opened file in current window
set autochdir

" Switch windows
noremap <C-w> <C-w>w
inoremap <C-w> <C-O><C-w>w
snoremap <C-w> <Esc><C-w>w
cnoremap <C-w> <C-O><C-w>w
onoremap <C-w> <C-O><C-w>w
"     in 'terminal' mode
tnoremap <C-w> <C-\><C-N><C-w>wi

" New tab
noremap <silent> <C-t> :tab sb<CR>
inoremap <silent> <C-t> <C-O>:tab sb<CR>
tnoremap <silent> <C-t> <C-\><C-N>:tab sb<CR>

" Switch tabs
noremap <silent> <C-PageUp> :tabprev<CR>
inoremap <silent> <C-PageUp> <C-O>:tabprev<CR>
snoremap <silent> <C-PageUp> <Esc>:tabprev<CR>
onoremap <silent> <C-PageUp> <C-O>:tabprev<CR>
"     in 'terminal' mode
tnoremap <silent> <C-PageUp> <C-\><C-N>:tabprev<CR>i

noremap <silent> <C-PageDown> :tabnext<CR>
inoremap <silent> <C-PageDown> <C-O>:tabnext<CR>
snoremap <silent> <C-PageDown> <Esc>:tabnext<CR>
onoremap <silent> <C-PageDown> <C-O>:tabnext<CR>
"     in 'terminal' mode
tnoremap <silent> <C-PageDown> <C-\><C-N>:tabnext<CR>i

" Close tab (like finish shell session in teminal)
noremap <C-d> :tabclose<CR>
inoremap <C-d> <C-O>:tabclose<CR>

" Close window without closing buffer
" TODO: do not close window when it is last window in tab
noremap <M-c> <C-w>c
inoremap <M-c> <C-O><C-w>c
cnoremap <M-c> <C-O><C-w>c
onoremap <M-c> <C-O><C-w>c
"     in 'terminal' mode
tnoremap <M-c> <C-\><C-N><C-w>c

function! s:IsCurrBufQuickFix()
    return &filetype == 'qf'
endfunction

function! s:IsCurrWindowQuickFix()
    return exists("w:quickfix_title")
endfunction

" Close buffer without closing window
" Do not switch to 'QuickFix' buffer on it's existence
function! NvimIdeCloseCurrBuf()
    if s:IsCurrWindowQuickFix()
        call NvimIdeClearAndCloseQuickfix()
        return
    endif

    let mruBufs = fzf#vim#_buflisted_sorted()

    if len(mruBufs) < 2
        throw "NvimIde: failed to close the only file buffer"
    endif

    if &modified
        throw "NvimIde: failed to close modified buffer"
    endif

    exe 'silent b' . mruBufs[1]
    exe 'bd ' . mruBufs[0]
endfunction

noremap <silent> <M-x> :call NvimIdeCloseCurrBuf()<CR>
inoremap <silent> <M-x> <C-O>:call NvimIdeCloseCurrBuf()<CR>

" Code window markers
function! NvimIdeMarkCodeWindow()
    let w:nvim_ide_is_code_window = v:true
endfunction

function! NvimIdeIsCodeWindow()
    if exists("w:nvim_ide_is_code_window")
        return w:nvim_ide_is_code_window
    endif

    return v:false
endfunction

augroup NvimIdeTabNewGroup
    autocmd!
    autocmd TabNew * call NvimIdeMarkCodeWindow()
augroup end

"     this function should be called on startup to mark initial window as 'code' window
call NvimIdeMarkCodeWindow()

" Split window vertically
noremap <silent> <M-\> <C-w>v:call NvimIdeMarkCodeWindow()<CR><C-w>w
inoremap <silent> <M-\> <C-O><C-w>v<C-o>:call NvimIdeMarkCodeWindow()<CR><C-o><C-w>w
tnoremap <silent> <M-\> <C-\><C-N><C-w>v:call NvimIdeMarkCodeWindow()<CR><C-w>wi

" Split windows horizontally
noremap <silent> <M--> <C-w>s:call NvimIdeMarkCodeWindow()<CR><C-w>w
inoremap <silent> <M--> <C-O><C-w>s<C-o>:call NvimIdeMarkCodeWindow()<CR><C-o><C-w>w
tnoremap <silent> <M--> <C-\><C-N><C-w>s:call NvimIdeMarkCodeWindow()<CR><C-w>wi

" Switch buffers
function! s:ShowFzfBuffers()
    function! Bufopen(lines) closure
        if len(a:lines) != 1
            return
        endif

        let b = matchstr(a:lines[0], '\[\zs[0-9]*\ze\]')
        execute 'buffer' b
    endfunction

    let sorted = fzf#vim#_buflisted_sorted()
    let header_lines = '--header-lines=' . (bufnr('') == get(sorted, 0, 0) ? 1 : 0)
    call fzf#run(fzf#wrap({'source': map(sorted, 'fzf#vim#_format_buffer(v:val)'),
                                   \ 'options': ['+m', '-x', '--tiebreak=index', header_lines, '--ansi', '-d', '\t', '--with-nth', '3..', '-n', '2,1..2',
                                   \             '--prompt', 'Buffers> ',
                                   \             '--info', 'inline',
                                   \             '--history', NvimIdeGetProjectExtraFilesDir() . '/fzf_buffers_history',
                                   \             '--bind', g:nvim_ide_fzf_bind],
                                   \ 'sink*': function('Bufopen')}))
endfunction

function! s:GotoCodeWindow()
    if NvimIdeIsCodeWindow()
        return v:true
    endif

    let initialWin = win_getid()
    let codeWin = -1
    while v:true
       wincmd w
       let currWin = win_getid()

       if NvimIdeIsCodeWindow()
           if codeWin == -1
               let codeWin = currWin
           else
               call NvimIdeEchoWarning("More than 1 code window. Switch to code window manually and choose the buffer.")
               call win_gotoid(initialWin)
               return v:false
           endif
       endif

       if currWin == initialWin
           break
       endif
    endwhile

    if codeWin == -1
        call NvimIdeEchoWarning("No code window(s).")
        call win_gotoid(initialWin)
        return v:false
    endif

    call win_gotoid(codeWin)
    return v:true
endfunction

function! NvimIdeBuffers()
    if !s:GotoCodeWindow()
        return
    endif

    let effectiveRoot = g:nvim_ide_start_dir
    if exists("g:nvim_ide_project_root")
        let effectiveRoot = g:nvim_ide_project_root
    endif

    let cwd_au = getcwd()
    exe 'lcd ' . effectiveRoot
    call s:ShowFzfBuffers()
    exe 'lcd ' . cwd_au
endfunction

" Switch buffers (should be in sync with fzf parts in tui/terminal)
inoremap <C-Up> <C-o>:call NvimIdeBuffers()<CR>
nnoremap <C-Up> :call NvimIdeBuffers()<CR>
snoremap <C-Up> <Esc>:call NvimIdeBuffers()<CR>
tnoremap <C-Up> <C-\><C-N>:call NvimIdeBuffers()<CR>
inoremap <C-Down> <C-o>:call NvimIdeBuffers()<CR>
nnoremap <C-Down> :call NvimIdeBuffers()<CR>
snoremap <C-Down> <Esc>:call NvimIdeBuffers()<CR>
tnoremap <C-Down> <C-\><C-N>:call NvimIdeBuffers()<CR>

" Win/Buf navigation using 'jumps'
" TODO: test 'set jumpoptions+=stack' behaviour

augroup NvimIdeWindows
    autocmd!
    autocmd VimEnter * clearjumps
augroup END

function! NvimIdeJumpPrev()
    if s:GotoCodeWindow()
        exe "normal! \<C-o>zz"
    endif
endfunction

function! NvimIdeJumpNext()
    if s:GotoCodeWindow()
        exe "normal! 1\<C-i>zz"
    endif
endfunction

noremap <M-left> :call NvimIdeJumpPrev()<CR>
inoremap <M-left> <C-o>: call NvimIdeJumpPrev()<CR>
smap <M-left> <Esc><M-left>

noremap <M-right> :call NvimIdeJumpNext()<CR>
inoremap <M-right> <C-o>: call NvimIdeJumpNext()<CR>
smap <M-right> <Esc><M-right>

" Quickfix window

" TODO: 'has_quickfix_movements' detection should be rewritten: set var local for quickfix buffer which indicates should we move farward or not

let s:has_quickfix_movements = 0
augroup NvimIdeQuickfix
    autocmd!
    autocmd BufReadPost quickfix let s:has_quickfix_movements = 0
augroup END

function! NvimIdeCenterText()
    normal! zz
endfunction

"     Next/prev in quickfix window. Quickfix is used for search results, 'LSP usage' results, etc.
function! NvimIdeJumpQFItem(f)
    exe a:f
    call NvimIdeCenterText()
endfunction

function! NvimIdeNextQFItem()
    let f = (s:has_quickfix_movements ? "cn" : "cc")

    call NvimIdeJumpQFItem(f)
    let s:has_quickfix_movements = 1
endfunction

"     item
nnoremap <M-down> :call NvimIdeNextQFItem()<CR>
inoremap <M-down> <C-o>:call NvimIdeNextQFItem()<CR>
smap <M-down> <Esc><M-down>
nnoremap <M-up> :call NvimIdeJumpQFItem("cp")<CR>
inoremap <M-up> <C-o>:call NvimIdeJumpQFItem("cp")<CR>
smap <M-up> <Esc><M-up>
"     file
nnoremap <C-M-down> :call NvimIdeJumpQFItem("cnf")<CR>
inoremap <C-M-down> <C-o>:call NvimIdeJumpQFItem("cnf")<CR>
smap <C-M-down> <Esc><C-M-down>
nnoremap <C-M-up> :call NvimIdeJumpQFItem("cpf")<CR>
inoremap <C-M-up> <C-o>:call NvimIdeJumpQFItem("cpf")<CR>
smap <C-M-up> <Esc><C-M-up>

"     Close quickfix window.
function! NvimIdeClearAndCloseQuickfix()
    let is_qf = s:IsCurrBufQuickFix()
    cclose
    cexpr []
    if is_qf
        wincmd p
    endif
endfunction

" TODO: possibly detection quickfix existence should be checked to close it under condition and move to insert mode
nnoremap <silent> <Esc> :call NvimIdeClearAndCloseQuickfix()<CR>i

function! NvimIdeQuickfixPostprocess(ptrn)
    call v:lua.NvimIdeHighlightQuickfix(a:ptrn)
    " Goto previous windows (probably with code)
    wincmd p
endfunction

" Tabline settings
function NvimIdeTabLine()
    let s = ''
    for i in range(tabpagenr('$'))
        " select the highlighting
        if i + 1 == tabpagenr()
            let s ..= '%#TabLineSel#'
        else
            let s ..= '%#TabLine#'
        endif

        " set the tab page number (for mouse clicks)
        let s ..= '%' .. (i + 1) .. 'T'

        " the label is made by NvimIdeTabLabel()
        let s ..= "\u258f" .. '%{NvimIdeTabLabel(' .. (i + 1) .. ')} '
    endfor

    " after the last tab fill with TabLineFill and reset tab page nr
    let s ..= '%#TabLineFill#%T'

    " right-align the label to close the current tab page
    if tabpagenr('$') > 1
        let s ..= '%=%#TabLine#%999XX'
    endif

    return s
endfunction

function NvimIdeTabLabel(n)
    let label = ''
    let bufnrlist = tabpagebuflist(a:n)

    " Add '+' if one of the buffers in the tab page is modified
    for bufnr in bufnrlist
        if getbufvar(bufnr, "&modified")
            let label = '+'
            break
        endif
    endfor

    " Append the number of windows in the tab page if more than one
    let wincount = tabpagewinnr(a:n, '$')
    if wincount > 1
        let label ..= wincount
    endif
    if label != ''
        let label ..= ' '
    endif

    " Append the buffer name
    return label ..  fnamemodify(bufname(bufnrlist[tabpagewinnr(a:n) - 1]), ':t')
endfunction

set tabline=%!NvimIdeTabLine()
